apply plugin: 'cpp'

model {
    platforms {
        x64 {
            architecture "x86_64"
        }
    }

    toolChains {
        gcc(Gcc) {
            path "/usr/local/bin"
            eachPlatform {
                cCompiler.executable = "gcc-7.3.0"
                cppCompiler.executable = "g++-7.3.0"
                linker.executable = "g++-7.3.0"
                staticLibArchiver.executable = "gcc-ar-7.3.0"
            }
        }
    }

    binaries {
        all {
            if (toolChain in Gcc) {
                cppCompiler.args '-Wall', '-std=c++14'
                linker.args '-L/usr/local/lib'
            }
        }
    }

    components {
        grammar_parser(NativeLibrarySpec) {
            targetPlatform "x64"
        }
        grammar_model(NativeLibrarySpec) {
            targetPlatform "x64"
        }
        grammar_utilities(NativeLibrarySpec) {
            targetPlatform "x64"

            // depends on grammar_model
        }
        reason(NativeLibrarySpec) {
            targetPlatform "x64"
        }
        command_line(NativeExecutableSpec) {
            targetPlatform "x64"

            binaries.all {
                lib library: "grammar_parser", linkage: 'static'
                lib library: "grammar_model", linkage: 'static'
                lib library: "grammar_utilities", linkage: 'static'
                lib library: "reason", linkage: 'static'
            }
        }
    }
}
